apply plugin: 'java'

repositories {
    mavenCentral();
}

apply plugin: 'signing'

signing {
    sign configurations.archives
}

def gitCommitSha = { ->
    def stdout = new ByteArrayOutputStream();
    exec {
        commandLine 'git', 'describe', '--long', '--always', '--dirty', '--exclude=*', '--abbrev=8';
        standardOutput = stdout;
    }
    return stdout.toString().trim();
}

def gitCommitShaLong = { ->
    def stdout = new ByteArrayOutputStream();
    exec {
        commandLine 'git', 'describe','--long','--always','--exclude=*','--abbrev=40';
        standardOutput = stdout;
    }
    return stdout.toString().trim();
}

java {
    sourceCompatibility = gradle.ext.javaVersion;
    targetCompatibility = gradle.ext.javaVersion;
}

apply plugin: 'maven-publish'

task javadocJar(type: Jar) {
    archiveClassifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'dk.yalibs'
            artifactId = 'yadi'
            // TODO: gitCommitSha postfix depends on build-type!
            version = '1.0.0+' + gitCommitSha();
            from components.java

            pom {
                name = 'yadi'
                description = 'Yet another framework-agnostic skyhook dependency injection implementation'
                url = 'https://www.yalibs.dk'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'yalibs'
                        name = 'Asger Gitz-Johansen'
                        email = 'yalibs@protonmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/yalibs/yadi.git'
                    developerConnection = 'scm:git:ssh://github.com/yalibs/yadi.git'
                    url = 'https://github.com/yalibs/yadi/'
                }
            }
        }
    }
}

