plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
}

def gitCommitSha = { ->
    def stdout = new ByteArrayOutputStream();
    exec {
        commandLine 'git', 'describe', '--long', '--always', '--dirty', '--exclude=*', '--abbrev=8';
        standardOutput = stdout;
    }
    return stdout.toString().trim();
}

def gitCommitShaLong = { ->
    def stdout = new ByteArrayOutputStream();
    exec {
        commandLine 'git', 'describe','--long','--always','--exclude=*','--abbrev=40';
        standardOutput = stdout;
    }
    return stdout.toString().trim();
}

def getVersion = { ->
    // TODO: gitCommitSha postfix depends on build-type!
    return '1.0.0+' + gitCommitSha();
}

group 'dk.yalibs'
version '1.0.0-SNAPSHOT'

repositories {
    mavenCentral()
}

ext {
    pomGroupId = group
    pomVersion = version
    pomArtifactId = "yadi"
    pomArtifactName = "yadi"
    jdkCompileVersion = 17
}

dependencies {
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.release = 17
}

sourceCompatibility = "${jdkCompileVersion}"
targetCompatibility = "${jdkCompileVersion}"

java {
    withJavadocJar()
    withSourcesJar()
    toolchain {
        languageVersion.set(JavaLanguageVersion.of("${jdkCompileVersion}"))
    }
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = "${pomGroupId}"
            artifactId = "${pomArtifactId}"
            version = "${pomVersion}"
            from components.java
            pom {
                name = "${pomArtifactId}"
                description = 'Yet another framework-agnostic skyhook dependency injection implementation'
                url = 'https://www.yalibs.dk'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'yalibs'
                        name = 'Asger Gitz-Johansen'
                        email = 'yalibs@protonmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/yalibs/yadi.git'
                    developerConnection = 'scm:git:ssh://github.com/yalibs/yadi.git'
                    url = 'https://github.com/yalibs/yadi/'
                }
            }
        }
    }
    repositories {
        maven {
            name = "OSSRH"
            def releasesRepoUrl = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            def snapshotsRepoUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = findProperty("ossrhUsername")
                password = findProperty("ossrhPassword")
            }
        }
    }
}

signing {
    def signingKey = findProperty("signingKey") ?: System.getenv("GPG_SIGNING_KEY")
    def signingPassword = findProperty("signingPassword") ?: System.getenv("GPG_SIGNING_PASSWORD")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.mavenJava
    // sign configurations.archives
}

